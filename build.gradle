plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.google.cloud.tools.jib' version '3.3.2'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-mail:3.2.5'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'

    implementation 'com.github.mwiede:jsch:0.2.17'

    // In-Memory-Cache
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    // Bucket4j
    implementation 'com.bucket4j:bucket4j-core:8.7.0'

    // JWT Dependencies
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Development Tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // API Documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // AWS S3
    implementation('com.amazonaws:aws-java-sdk-s3:1.12.771') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

test {
    useJUnitPlatform()
}

bootJar {
    archiveBaseName.set('playBaseballServer')
}

jar {
    archiveBaseName.set('playBaseballServer')
}

def activeProfile = project.hasProperty('spring.profiles.active') ? project.property('spring.profiles.active') : 'prod'

jib {
    from {
        image = 'eclipse-temurin:21-jre'
    }
    to {
        image = 'seung093/playbaseball:v2'
    }
    container {
        ports = ['8080']
        creationTime = 'USE_CURRENT_TIMESTAMP'
        jvmFlags = [
                "-Dspring.profiles.active=${activeProfile}",
        ]
    }
    if (activeProfile == 'dev' || activeProfile == 'docker') {
        extraDirectories {
            paths = [
                    file('secrets')
            ]
        }
    }
    containerizingMode = 'packaged'
}

// 추가: 빌드 시 사용된 프로필 출력
tasks.register('printActiveProfile') {
    doLast {
        println "Active profile: ${activeProfile}"
    }
}

// Jib 태스크 실행 전에 프로필 출력 태스크 실행
tasks.jib.dependsOn tasks.printActiveProfile
tasks.jibDockerBuild.dependsOn tasks.printActiveProfile